<?xml version="1.0" encoding="utf-8"?>
<s:TitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009"
			   xmlns:s="library://ns.adobe.com/flex/spark"
			   xmlns:mx="library://ns.adobe.com/flex/mx"
			   xmlns:tools="il.co.topq.systems.report.views.components.reports.tools.*"
			   xmlns:grids="il.co.topq.systems.report.views.components.reports.grids.*"
			   width="942" height="720" chromeColor="#D4E4FB" close="cancel_clickHandler(event)"
			   color="#000000" creationComplete="onCreationComplete(event)" title="{reportTitle}" xmlns:common="il.co.topq.systems.report.views.components.reports.common.*">
	
	<fx:Script>
		<![CDATA[
			import il.co.topq.systems.report.events.test.GetTestsByFilterEvent;
			import il.co.topq.systems.report.events.test.GetTestsByFilterSizeEvent;
			import il.co.topq.systems.report.events.testCustomReport.ExportTestCustomReportEvent;
			import il.co.topq.systems.report.models.ReportModelLocator;
			import il.co.topq.systems.report.models.valueobjects.*;
			
			import mx.collections.ArrayCollection;
			import mx.collections.ArrayList;
			import mx.controls.Alert;
			import mx.controls.ComboBox;
			import mx.events.FlexEvent;
			import mx.events.ListEvent;
			import mx.formatters.DateFormatter;
			import mx.managers.PopUpManager;
			
			import org.as3commons.collections.Map;
			
			import spark.components.HGroup;
			import spark.components.Label;
			import spark.events.IndexChangeEvent;
			
			[Bindable]
			public var testCustomReportGridModel:TestCustomReportGridModel = ReportModelLocator.getTestCustomReportGridModel();
			
			[Bindable]
			private var reportTitle:String = "Test Custom Report";
			
			protected var comboBoxPropertyValueArr:Array; //propertyComboBox's
			
			protected var  ANY_VALUE_INDEX:Number = 0;			
			protected var timeRangeChooser:TimeRangeChooser;
			
			protected function cancel_clickHandler(event:Event):void
			{
				PopUpManager.removePopUp(this);
			}	
			
			/**
			 * This method will set the custom report title according to the 
			 * last filtered timeRange values.
			 * in case no filter was made the defult will be preseted. 
			 **/
			protected function setCustomReportTitle():void{
				
				var title:String = "Test Custom Report";
				
				if (timeRangeChooser != null){
					testCustomReportGridModel.testQuery.timeRange = timeRangeChooser.getFilter().timeRange;
					
					if ((timeRangeChooser.lowerDate.selectedDate != null) && (timeRangeChooser.upperDate.selectedDate != null)){
						var from:String = formatDate(timeRangeChooser.lowerDate.selectedDate.getTime());
						var to:String = formatDate(timeRangeChooser.upperDate.selectedDate.getTime());
						title += " between " + from + " and " + to ;
					}
				}
				reportTitle = title;
			}
			
			protected function formatDate(date:Number):String{
				var df:DateFormatter = new DateFormatter();   
				df.formatString = "DD/MM/YY";  
				var d:Date = new Date(date);   
				return df.format(d);
			}
			
			protected function handleNextClicked(event:MouseEvent):void{
				Alert.show("Need to inherit and override this method");
			}
			
			protected function handlePreviousClicked(event:MouseEvent):void{
				Alert.show("Need to inherit and override this method");
			}
			
			protected function setPropertiesInTestQuery():void{
				
				testCustomReportGridModel.testQuery.testProperties = new ArrayCollection();//will reset test query properties.
				
				for (var i:int=0; i<comboBoxPropertyValueArr.length; i++){
					var c:ComboBox = comboBoxPropertyValueArr[i] as ComboBox;
					
					if (c.selectedIndex != ANY_VALUE_INDEX){
						testCustomReportGridModel.testQuery.testProperties.addItem(new TestProperty(c.name,c.selectedItem as String));					
					}else{
						testCustomReportGridModel.testQuery.testProperties.addItem(new TestProperty(c.name,""));						
					}
				}
			}
			
			/**
			 * This method will add event listners to the Navigation component.
			 * Will listen on next | previous clicks.
			 **/
			private function addNavigationListners():void {
				navigation.LinkButtonPreviousLabel.addEventListener(MouseEvent.CLICK,handlePreviousClicked);
				navigation.LinkButtonPreviousIcon.addEventListener(MouseEvent.CLICK,handlePreviousClicked);
				navigation.LinkButtonNextLabel.addEventListener(MouseEvent.CLICK,handleNextClicked);
				navigation.LinkButtonNextIcon.addEventListener(MouseEvent.CLICK,handleNextClicked);
			}
			
			protected function onCreationComplete(event:FlexEvent):void
			{				
				addNavigationListners();
				populatePropertValuesComboBox();
				setPropertiesInTestQuery();
				getResults();
			}
			
			private function populatePropertValuesComboBox():void {
				comboBoxPropertyValueArr = new Array();				
				var numOfProperties:Number = testCustomReportGridModel.testCustomReportPropertiesValues.length;
				for (var i:int=0; i<numOfProperties; i++){
					var propertyValuesWrapper:PropertyValuesWrapper = (testCustomReportGridModel.testCustomReportPropertiesValues[i]) as PropertyValuesWrapper;
					propertyValuesWrapper.propertyValues.addItemAt("All",0);
					
					var hgroup:HGroup = new HGroup();
					hgroup.name="group"+i;
					hgroup.width=300;
					hgroup.height=35;
					hgroup.paddingLeft=25;
					hgroup.paddingTop=10;
					hgroup.verticalAlign="middle";
					var label:Label = new Label();				
					label.text = propertyValuesWrapper.propertyKey;
					var comboBox:mx.controls.ComboBox = new mx.controls.ComboBox();
					comboBox.width=142;
					comboBox.name=label.text;
					comboBox.editable = false;
					comboBoxPropertyValueArr.push(comboBox);	
					comboBox.dataProvider = propertyValuesWrapper.propertyValues;
					comboBox.itemRenderer = new ClassFactory (mx.controls.Label);
					comboBox.addEventListener("change",combobox_changeHandler);
					hgroup.addElement(label);
					hgroup.addElement(comboBox);
					propertiesContainer.addElement(hgroup);
				}
			}
			
			protected function onGetResultsClick(event:MouseEvent):void
			{			
				getResults();
			}
			
			/**
			 * This method will be called once save is clicked in the select Time range pop up.
			 **/
			public function saveTimeRangeClicked(event:MouseEvent):void{			
				getTimeRangeData();
				PopUpManager.removePopUp(timeRangeChooser);
				getResults();
			}
			
			private function getTimeRangeData():void{
				testCustomReportGridModel.testQuery.timeRange = timeRangeChooser.getFilter().timeRange;
			}
			
			public function cleanTimeRangeClicked(event:MouseEvent):void{			
				getTimeRangeData();
				PopUpManager.removePopUp(timeRangeChooser);
				getResults();
			}

			protected function tools_changeHandler(event:IndexChangeEvent):void
			{
				Alert.show("Need to inherit and override this method");
			}
			
			protected function combobox_changeHandler(event:ListEvent):void
			{
				getResults();
			}
			
			protected function getResults():void{
				Alert.show("Need to inherit and override this method");
			}
			
		]]>
	</fx:Script>
	
	<s:Group id="propertiesContainer" x="9" y="9" width="921" height="127">
		<s:layout>
			<s:TileLayout orientation="rows"/>
		</s:layout>
		
	</s:Group>
	<s:HGroup x="0" y="124" width="100%" height="10%" horizontalAlign="center" paddingLeft="20"
			  paddingRight="20" verticalAlign="middle">
		<s:HGroup width="50%" height="100%" horizontalAlign="left" paddingRight="10"
				  verticalAlign="middle">
		</s:HGroup>
		<s:HGroup width="50%" height="100%" horizontalAlign="right" paddingRight="10"
				  verticalAlign="middle">
			<tools:FilteredTestCustomReportTool id="tools" height="40%"
											change="tools_changeHandler(event)" selectedItem="Tools"/>
			<s:Button x="468" y="221" buttonMode="true" click="onGetResultsClick(event)"
					  mouseChildren="false"
					  skinClass="il.co.topq.systems.report.views.skin.RefreshButtonSkin"
					  toolTip="Get Results" useHandCursor="true"/>
		</s:HGroup>
	</s:HGroup>
	<s:VGroup x="10" y="178" width="920" height="442">
		<grids:TestsReportGrid id="testReportGrid" height="430" dataProvider="{testCustomReportGridModel.gridData.items}">
			
		</grids:TestsReportGrid>
	</s:VGroup>
	<s:Group x="11" y="628" width="919" height="48">
		<common:Navigation paddingBottom="8" width="100%" paddingTop="8" verticalAlign="middle" horizontalAlign="center"
						   id="navigation" navModel="{testCustomReportGridModel.navigation}"/>
	</s:Group>
	
</s:TitleWindow>
